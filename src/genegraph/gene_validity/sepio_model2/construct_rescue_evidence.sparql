prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>
prefix cg: <http://dataexchange.clinicalgenome.org/terms/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

construct {
  ?evidenceLine  a ?evidenceLineType ;
  dc:description ?scoreDescription ;
  cg:evidence ?evidenceItem ;
  cg:scoreStatus ?scoreStatus ;
  cg:calculatedScore ?calculatedScore ;
  cg:strengthScore ?score .

  ?evidenceItem  a ?evidenceItemType ;
  dc:description ?evidenceDescription ;
  dc:source ?article ;
  rdfs:label ?evidenceLabel .
}
where {

  ?evidenceLine a gci:evidenceScore ;
  gci:calculatedScore ?calculatedScore ;
  gci:scoreStatus ?scoreStatus ;
  gci:affiliation ?affiliation .

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?scoreDescription .
  }

  OPTIONAL  {
    ?evidenceLine gci:score ?adjustedScore .
  }
  
  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?score) .
  
  ?evidenceItem gci:scores ?evidenceLine ;
  gci:rescue ?gciRescue  ;
  gci:label ?evidenceLabel .

  OPTIONAL {
    ?gciRescue gci:explanation ?evidenceDescription .
  }
  ?gciRescue gci:rescueType ?gciType .

  ?evidenceLineType gcixform:hasGCIType ?gciType ;
  gcixform:hasEvidenceItemType ?evidenceItemType ;
  gcixform:usedIn gcixform:Rescue .

  ?annotation gci:experimentalData ?evidenceItem ;
  gci:article ?publication .
  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .  
 
}
